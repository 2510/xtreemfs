; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "XtreemFS Windows Client"
#define MyAppVersion "1.3.1.96"
#define MyAppPublisher "XtreemFS Project"
#define MyAppURL "http://www.XtreemFS.org/"
#define MyAppExeName "MyProg.exe"

#define SVNRoot "..\..\..\"
#define CbFSAdapterBuildDir SVNRoot + "cpp\build\Debug\"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{EA8FA8CB-02C9-4028-8CBC-C109F9B8DFFA}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
LicenseFile={#SVNRoot}LICENSE
OutputBaseFilename=xtreemfs_windows_client_{#MyAppVersion}
SetupIconFile=xtreemfs_logo_transparent.ico
Compression=lzma
SolidCompression=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

; Entries in Startmenu Folder
[Icons]
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"

[Files]
Source: "{#CbFSAdapterBuildDir}mount.xtreemfs.exe"; DestDir: "{app}"; Flags: ignoreversion; BeforeInstall: InstallCbFS
Source: "{#CbFSAdapterBuildDir}mount.xtreemfs.pdb"; DestDir: "{app}"; Flags: ignoreversion;
Source: "{#CbFSAdapterBuildDir}mkfs.xtreemfs.exe"; DestDir: "{app}"; Flags: ignoreversion;
Source: "{#CbFSAdapterBuildDir}rmfs.xtreemfs.exe"; DestDir: "{app}"; Flags: ignoreversion;
Source: "{#CbFSAdapterBuildDir}lsfs.xtreemfs.exe"; DestDir: "{app}"; Flags: ignoreversion;
Source: "{#CbFSAdapterBuildDir}libeay32.dll"; DestDir: "{app}"; Flags: ignoreversion;
Source: "{#CbFSAdapterBuildDir}ssleay32.dll"; DestDir: "{app}"; Flags: ignoreversion;
Source: "cbfsinst.dll"; Flags: dontcopy
Source: "cbfs.cab"; Flags: dontcopy
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Code]
var
  RebootNeeded: DWORD;

function RunCbFSInstaller(CabPathName, ProductName: String; SupportPnP: Integer; ModulesToInstall: DWORD; var RebootNeeded: DWORD) : Integer;
external 'InstallW@files:cbfsinst.dll,cbfs.cab stdcall setuponly';

procedure InstallCbFS();
var
  InstallerResult: Integer;
begin
  Log('Using CBFS archive: ' + ExpandConstant('{tmp}') + '\' + 'cbfs.cab');
  InstallerResult := RunCbFSInstaller(ExpandConstant('{tmp}') + '\' + 'cbfs.cab', 'XtreemFS Windows Client', 1, 196609, RebootNeeded);
  if InstallerResult <> 1 then
    MsgBox('Failed to install the CBFS driver. Please reboot your system and try again.', mbCriticalError, MB_OK);

end;

function NeedRestart(): Boolean;
begin
  If RebootNeeded = 1 then
    Result := True
  else
    Result := False;
end;